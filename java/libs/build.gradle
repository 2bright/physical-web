apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

repositories {
    jcenter()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

dependencies {
    compile 'org.json:json:20140107'
    testCompile 'junit:junit:4.11'
    testCompile 'org.skyscreamer:jsonassert:1.2.3'
}

findbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

findbugs {
    reportsDir = file("$project.buildDir/reports/findbugs")
    excludeFilter = file("$project.rootDir/config/findbugs/exclude-filter.xml")
}

task checkstyle(type: Checkstyle) {
    configProperties.checkstyleSuppressionsPath = new File(rootDir, "config/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

task pmd(type: Pmd) {
    ignoreFailures = false
    ruleSetFiles = files("${project.rootDir}/config/pmd/pmd-ruleset.xml")
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$project.buildDir/reports/pmd/pmd.xml"
        }
        html {
            destination "$project.buildDir/reports/pmd/pmd.html"
        }
    }
}

check.dependsOn 'checkstyle', 'pmd'
