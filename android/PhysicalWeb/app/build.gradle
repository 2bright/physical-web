apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.0.2"

    //auto versioning logic
    def versionPropertiesFile = file('version.properties')
    if (versionPropertiesFile.canRead()) {
        def Properties properties = new Properties()
        properties.load(new FileInputStream(versionPropertiesFile))
        def major = properties['MAJOR'].toInteger()
        def minor = properties['MINOR'].toInteger()
        def build = properties['BUILD'].toInteger() + 1
        properties['BUILD'] = build.toString()
        properties.store(versionPropertiesFile.newWriter(), null)

        defaultConfig {
            applicationId "physical_web.org.physicalweb"
            minSdkVersion 18
            targetSdkVersion 21
            versionCode 4
            versionName "${major}.${minor}.${build}"
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories{
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'com.android.volley:volley@aar'
    compile 'org.uribeacon:uribeacon-library-release@aar'
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile files('libs/google-api-client-1.19.0.jar')
    compile files('libs/google-http-client-1.19.0.jar')
    compile files('libs/google-api-services-urlshortener-v1-rev33-1.19.0.jar')
    compile files('libs/google-http-client-android-1.19.0.jar')
}
